CREATE OR REPLACE FUNCTION CALCULO_PRESUPUESTO_INTER( ANYO IN   NUMBER,
   NUM_EXP IN NUMBER, NUM_INTERPRETES IN NUMBER, HORAS_INTERPRETACION IN VARCHAR2, ID_TAREA IN NUMBER) RETURN NUMBER IS

IMPORTE NUMBER(8,2):=0.0;
IMPORTE_BASE NUMBER(8,2):=0.0;
IMPORTE_IVA NUMBER(8,2):=0.0; 

IMPORTE_ENTIDAD        NUMBER(9,2):=0.0;
IMPORTE_BASE_ENTIDAD   NUMBER(8,2):=0.0;
IMPORTE_IVA_ENTIDAD    NUMBER(8,2):=0.0;
IMPORTE_BASE_TOTAL NUMBER(8,2):=0.0;

TYPE r_reg_interpretacion IS RECORD (
      TIPO_ACTO NUMBER(2) NULL, 
      TIPO_FACTURACION VARCHAR2(1) NULL,
      IND_PROGRAMADA VARCHAR2(1) NULL,
      TIPO_PETICIONARIO VARCHAR2(1) NULL,
      IND_CAE VARCHAR2(1) NULL
);
INTERPRETACION r_reg_interpretacion;

TYPE r_reg_orden IS RECORD (
     ID_ORDEN NUMBER(4),
    JORN_COMP_HORAS_DESDE NUMBER(1),
    JORN_COMP_HORAS_HASTA NUMBER(1) ,
    JORN_MEDIA_HORAS_DESDE NUMBER(1) ,
    JORN_MEDIA_HORAS_HASTA NUMBER(1) ,
    PREC_MIN_CAE NUMBER(6,2) ,
    PREC_MIN_NO_CAE NUMBER(6,2) ,
    PREC_HORA_REUNION_CAE NUMBER(5,2) ,
    PREC_HORA_REUNION_NO_CAE NUMBER(5,2) ,
    PREC_JORN_CONGRESO_EU NUMBER(6,2) ,
    PREC_MEDIA_CONGRESO_EU NUMBER(6,2) ,
    PREC_HORA_CONGRESO_EU NUMBER(5,2) ,
    PREC_JORN_CONGRESO_NO_EU NUMBER(6,2) ,
    PREC_MEDIA_CONGRESO_NO_EU NUMBER(6,2) ,
    PREC_HORA_CONGRESO_NO_EU NUMBER(5,2),
    POR_IVA NUMBER(3)
);
ORDEN_PRECIOS r_reg_orden;

TYPE r_reg_config_facturacion IS RECORD (
   IND_IVA VARCHAR2(1),
   IND_FACTURABLE VARCHAR2(1)
);
CONFIG_FACTURACION r_reg_config_facturacion;

TIPO_FACTURACION_INTERPRETE VARCHAR2(1):= 'I';
TIPO_FACTURACION_HORA VARCHAR2(1):= 'H';

HORAS NUMBER(7):=0;
MINUTOS NUMBER(2):=0;

N_JORNADAS_COMPLETAS NUMBER(3):= 0;
N_JORNADAS_MEDIAS NUMBER(3):= 0;

CONTADOR NUMBER(1):=0;

-- SELECCIONAMOS LAS ENTIDADES DE FACTURACION DEL EXPEDIENTE;
  CURSOR C_ENTIDADES_FACT
  IS
    (SELECT ID_058,
      TIPO_ENTIDAD_ASOC_058,
      ID_ENTIDAD_ASOC_058,
      DNI_CONTACTO_058,
      POR_FACTURA_058,
      ent.FACTURABLE                 AS FACTURABLE,
      ent.IVA                        AS IVA,
      NVL(sol.CDIRNORA,ent.CDIRNORA) AS DIRNORA,
      DECODE(ID_098, NULL, 'N', 'S') AS TIENE_DATOS
    FROM AA79B58T00,
      X54JAPI_ENTIDADES_SOLIC ent,
      X54JAPI_SOLICITANTES sol,
      AA79B98T00
    WHERE TIPO_ENTIDAD_ASOC_058 = ent.TIPO
    AND ID_ENTIDAD_ASOC_058     = ent.CODIGO
    AND DNI_CONTACTO_058        = sol.DNI (+)
    AND ID_058                  = ID_098 (+)
    AND ANYO_058                = ANYO
    AND NUM_EXP_058             = NUM_EXP
    );

BEGIN
	
	BEGIN
	-- seleccionamos la orden de precios publicos, lo referente a la interpretación
	select ID_ORDEN_035, JORN_COMP_HORAS_DESDE_035, JORN_COMP_HORAS_HASTA_035, JORN_MEDIA_HORAS_DESDE_035, JORN_MEDIA_HORAS_HASTA_035 ,
                PREC_MIN_CAE_035, PREC_MIN_NO_CAE_035, PREC_HORA_REUNION_CAE_035, PREC_HORA_REUNION_NO_CAE_035, PREC_JORN_CONGRESO_EU_035,
                PREC_MEDIA_CONGRESO_EU_035,  PREC_HORA_CONGRESO_EU_035, PREC_JORN_CONGRESO_NO_EU_035, PREC_MEDIA_CONGRESO_NO_EU_035,
                PREC_HORA_CONGRESO_NO_EU_035, POR_IVA_031 into ORDEN_PRECIOS from AA79B35T00, AA79B30T00, AA79B31T00 
                WHERE ID_030 = ID_ORDEN_031 AND ID_030 = ID_ORDEN_035 (+) AND IND_VIGOR_030 = 'S' AND IND_VIGENTE_031 = 'S';
                
	-- las horas interpretación nos llegan en formato hh:mm las pasamos a horas redondeando
	HORAS:= TO_NUMBER(SUBSTR(HORAS_INTERPRETACION, 1, INSTR(HORAS_INTERPRETACION, ':')-1));
	MINUTOS:= TO_NUMBER(SUBSTR(HORAS_INTERPRETACION, INSTR(HORAS_INTERPRETACION, ':')+1));
	IF (MINUTOS > 0 ) THEN
		HORAS:= HORAS + 1;
	END IF;
	
	-- obtenemos el tipo de acto
	SELECT TIPO_ACTO_052, TIPO_FACTURACION_008, IND_PROGRAMADA_052, TIPO_PETICIONARIO_052, DECODE(CODPROV_049,'20','S','48','S','01','S','N')  into INTERPRETACION 
	FROM AA79B52T00, AA79B08T00, AA79B49T00 WHERE ANYO_052 = ANYO AND NUM_EXP_052 = NUM_EXP AND TIPO_ACTO_052 = ID_008 AND DIR_NORA_052 = CDIRNORA_049; 
	
	IF (INTERPRETACION.TIPO_FACTURACION = TIPO_FACTURACION_INTERPRETE) THEN
		-- Miramos cuantas jornadas completas enteras hay según el rango de jornadas completas definido.
		N_JORNADAS_COMPLETAS := TRUNC(HORAS/ORDEN_PRECIOS.JORN_COMP_HORAS_HASTA);
		IF (N_JORNADAS_COMPLETAS = 0) THEN
		 -- comprobamos si son horas dentro del rango de jornada completa
		 IF (HORAS >= ORDEN_PRECIOS.JORN_COMP_HORAS_DESDE AND HORAS <= ORDEN_PRECIOS.JORN_COMP_HORAS_HASTA) THEN
		 	N_JORNADAS_COMPLETAS := 1;	
		 	HORAS:= 0; -- no quedan horas pendientes de tarificar
		 END IF;
		ELSE
			HORAS:= HORAS - (N_JORNADAS_COMPLETAS * ORDEN_PRECIOS.JORN_COMP_HORAS_HASTA);
		END IF;
		-- miramos cuantas medias jornadas hay
		N_JORNADAS_MEDIAS := TRUNC(HORAS/ORDEN_PRECIOS.JORN_MEDIA_HORAS_HASTA);
		IF (N_JORNADAS_MEDIAS = 0) THEN
			-- comprobamos si son horas dentro del rango de jornada media
			IF (HORAS >= ORDEN_PRECIOS.JORN_MEDIA_HORAS_DESDE AND HORAS <= ORDEN_PRECIOS.JORN_MEDIA_HORAS_HASTA) THEN
		 		N_JORNADAS_MEDIAS := 1;	
		 		HORAS:= 0; -- no quedan horas pendientes de tarificar
		 	END IF;
		ELSE
			HORAS:= HORAS - (N_JORNADAS_MEDIAS * ORDEN_PRECIOS.JORN_MEDIA_HORAS_HASTA);
		END IF;
		
		IF (INTERPRETACION.IND_PROGRAMADA = 'S' AND INTERPRETACION.TIPO_PETICIONARIO = 'A') THEN
			IMPORTE:= (N_JORNADAS_COMPLETAS*ORDEN_PRECIOS.PREC_JORN_CONGRESO_EU) + (N_JORNADAS_MEDIAS*ORDEN_PRECIOS.PREC_MEDIA_CONGRESO_EU) + (HORAS*ORDEN_PRECIOS.PREC_HORA_CONGRESO_EU);
		ELSE
			IMPORTE:= (N_JORNADAS_COMPLETAS*ORDEN_PRECIOS.PREC_JORN_CONGRESO_NO_EU) + (N_JORNADAS_MEDIAS*ORDEN_PRECIOS.PREC_MEDIA_CONGRESO_NO_EU) + (HORAS*ORDEN_PRECIOS.PREC_HORA_CONGRESO_NO_EU);
		END IF;
	ELSE
		IF (INTERPRETACION.IND_CAE = 'S') THEN
			IMPORTE:= HORAS*ORDEN_PRECIOS.PREC_HORA_REUNION_CAE;
		ELSE
			IMPORTE:= HORAS*ORDEN_PRECIOS.PREC_HORA_REUNION_NO_CAE;
    	END IF;
	END IF;
	
	IMPORTE:= IMPORTE*NUM_INTERPRETES;
	
	IF (INTERPRETACION.IND_CAE = 'S') THEN
		IF (IMPORTE < ORDEN_PRECIOS.PREC_MIN_CAE) THEN
			IMPORTE:= ORDEN_PRECIOS.PREC_MIN_CAE;
		END IF;
	ELSE
		IF (IMPORTE < ORDEN_PRECIOS.PREC_MIN_NO_CAE) THEN
			IMPORTE:= ORDEN_PRECIOS.PREC_MIN_NO_CAE;
		END IF;
	END IF;
	
    IMPORTE_BASE_TOTAL:= round((IMPORTE * 100 / (100 + ORDEN_PRECIOS.POR_IVA)),3);
    
    IMPORTE_IVA:= 0.0;
	IMPORTE:= 0.0;
IMPORTE_BASE:= 0;
    FOR R_ENTIDADES_FACT IN C_ENTIDADES_FACT LOOP
		
		IMPORTE_BASE_ENTIDAD:= round((IMPORTE_BASE_TOTAL * R_ENTIDADES_FACT.POR_FACTURA_058)/100,2);
		IMPORTE_IVA_ENTIDAD:= 0.0;
		IF (R_ENTIDADES_FACT.IVA = 'S') THEN
			IMPORTE_IVA_ENTIDAD:= (IMPORTE_BASE_ENTIDAD * ORDEN_PRECIOS.POR_IVA) / 100;
		END IF;
		IMPORTE_ENTIDAD:= IMPORTE_IVA_ENTIDAD + IMPORTE_BASE_ENTIDAD;
		
		IMPORTE_BASE      := IMPORTE_BASE       + IMPORTE_BASE_ENTIDAD;
		IMPORTE_IVA:= IMPORTE_IVA + IMPORTE_IVA_ENTIDAD;
		IMPORTE:= IMPORTE + IMPORTE_ENTIDAD;
		
	END LOOP;
	
	SELECT count(1) into CONTADOR FROM AA79B89S01 WHERE ID_TAREA_089=ID_TAREA;
	IF (CONTADOR > 0) THEN
	    UPDATE AA79B89T00 set PRESUPUESTO_089 = IMPORTE, IMPORTE_BASE_089 = IMPORTE_BASE,
	    IMPORTE_IVA_089 = IMPORTE_IVA,
	    POR_IVA_089 = ORDEN_PRECIOS.POR_IVA,  
	    ID_ORDEN_089 = ORDEN_PRECIOS.ID_ORDEN,
	    NUM_INTERPRETES_089 = NUM_INTERPRETES,
	    HORAS_INTERPRETACION_089 = HORAS_INTERPRETACION,
	    IND_VISIBLE_089 = 'N'
	    WHERE ID_TAREA_089 = ID_TAREA;
	ELSE
		INSERT INTO AA79B89S01 (ID_TAREA_089, NUM_INTERPRETES_089, HORAS_INTERPRETACION_089, PRESUPUESTO_089, IMPORTE_BASE_089, IMPORTE_IVA_089, POR_IVA_089, ID_ORDEN_089)
		VALUES (ID_TAREA, NUM_INTERPRETES, HORAS_INTERPRETACION, IMPORTE, IMPORTE_BASE,  IMPORTE_IVA, ORDEN_PRECIOS.POR_IVA, ORDEN_PRECIOS.ID_ORDEN);
	END IF;
    
	EXCEPTION
       WHEN NO_DATA_FOUND THEN
           IMPORTE := -1;
   END;
	
	
	RETURN IMPORTE;
END;
/