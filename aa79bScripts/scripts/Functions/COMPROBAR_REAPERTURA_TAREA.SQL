create or replace FUNCTION COMPROBAR_REAPERTURA_TAREA ( IDTAREA IN NUMBER ) RETURN NUMBER IS

	RETORNO                     NUMBER(1):= 0; -- 0, Es posible reabrir la tarea; 1, En de que no sea posible reabrir la tarea
	TIPO_EXPEDIENTE             VARCHAR2(1);
	TIPO_INTERPRETACION         VARCHAR2(1):= 'I';
	TIPO_TRADUCCION             VARCHAR2(1):= 'T';
	TIPO_REVISION               VARCHAR2(1):= 'R';
	TAREA_NOTIF_CORRECC_PROV    NUMBER(2):= 13;
	TAREA_ENTREGA_TRADUCCION    NUMBER(2):= 19;
    TAREA_TRADUCCION_ENTREGA    NUMBER(2):= 22;
	TAREA_ENTREGA_REVISION      NUMBER(2):= 20;
	TAREA_CORREDACCION          NUMBER(2):= 3;
	TAREA_NO_CONF_CLIENTE       NUMBER(2):= 9;
    TAREA_NO_CONF_PROVEEDOR     NUMBER(2):= 5;
    TAREA_NO_CONF_TRADUCTOR     NUMBER(2):= 23;
	ESTADO_EXP_EN_CURSO         NUMBER(2):= 3;
	ESTADO_EXP_CERRADO          NUMBER(2):= 6;
	ESTADO_TAREA_EJECUTADA		NUMBER(1):= 3;
	ORDEN_TAREA_ENTREGA         NUMBER(2):= 0;
	CONTADOR 					NUMBER(2):= 0;

	TYPE r_reg_tarea IS RECORD (
		ANYO NUMBER(4),
	    NUM_EXP NUMBER(6),
	    ID_TIPO_TAREA NUMBER(2),
	    ORDEN NUMBER(2),
	    IND_NO_CONFORMIDAD VARCHAR2(1),
	    IND_REQ_CIERRE VARCHAR2(1)
	);
	TAREA r_reg_tarea;

	TYPE r_reg_expediente IS RECORD (
		ID_TIPO_EXPEDIENTE VARCHAR2(1),
	    ID_ESTADO_EXP NUMBER(2),
	    ID_FASE_EXPEDIENTE NUMBER(2)
	);
	EXPEDIENTE r_reg_expediente;

BEGIN

	SELECT ANYO_081, NUM_EXP_081, ID_TIPO_TAREA_081, ORDEN_081, IND_NO_CONFORMIDAD_081, IND_REQ_CIERRE_015 into TAREA FROM AA79B81T00 LEFT JOIN AA79B15S01 ON ID_TIPO_TAREA_081 = ID_015 WHERE ID_TAREA_081 = IDTAREA;

	-- Si la tarea es notificación de correcciones al proveedor, no se puede reabrir.
	IF (TAREA.ID_TIPO_TAREA = TAREA_NOTIF_CORRECC_PROV) THEN
		RETORNO := 1;
	ELSE
		SELECT ID_TIPO_EXPEDIENTE_051, ID_ESTADO_EXP_059, ID_FASE_EXPEDIENTE_059 INTO EXPEDIENTE FROM AA79B51S01
		LEFT JOIN AA79B59S01 ON ANYO_051 = ANYO_059 AND NUM_EXP_051 = NUM_EXP_059 AND ESTADO_BITACORA_051 = ID_ESTADO_BITACORA_059
		WHERE ANYO_051 = TAREA.ANYO AND NUM_EXP_051 = TAREA.NUM_EXP;

		IF ( EXPEDIENTE.ID_TIPO_EXPEDIENTE = TIPO_INTERPRETACION ) THEN
			IF ( EXPEDIENTE.ID_ESTADO_EXP = ESTADO_EXP_CERRADO AND TAREA.IND_REQ_CIERRE = 'S' ) THEN
				RETORNO := 1;
			END IF;
		ELSIF ( EXPEDIENTE.ID_TIPO_EXPEDIENTE = TIPO_TRADUCCION ) THEN
			IF ( EXPEDIENTE.ID_ESTADO_EXP = ESTADO_EXP_EN_CURSO ) THEN
				IF ( (TAREA.ID_TIPO_TAREA = TAREA_ENTREGA_TRADUCCION OR TAREA.ID_TIPO_TAREA =TAREA_TRADUCCION_ENTREGA) AND TAREA.IND_NO_CONFORMIDAD = 'S' ) THEN
						RETORNO := 1;
				ELSE
					SELECT COUNT(1) into contador from AA79B81T00 WHERE ID_TAREA_REL_081 = IDTAREA AND ID_TIPO_TAREA_081 IN (TAREA_NO_CONF_CLIENTE,TAREA_NO_CONF_PROVEEDOR,TAREA_NO_CONF_TRADUCTOR)
					AND ESTADO_EJECUCION_081 <> ESTADO_TAREA_EJECUTADA;
					if (CONTADOR > 0) THEN
						RETORNO := 1;
					ELSE
						SELECT NVL(MAX(ORDEN_081), 0) INTO ORDEN_TAREA_ENTREGA FROM AA79B81T00
						WHERE (ID_TIPO_TAREA_081 = TAREA_ENTREGA_TRADUCCION OR ID_TIPO_TAREA_081 = TAREA_TRADUCCION_ENTREGA) AND IND_NO_CONFORMIDAD_081 = 'S' AND ANYO_081 = TAREA.ANYO AND NUM_EXP_081 = TAREA.NUM_EXP;

						IF ( ORDEN_TAREA_ENTREGA <> 0 AND TAREA.ORDEN < ORDEN_TAREA_ENTREGA ) THEN
							RETORNO := 1;
						END IF;
					END IF;
				END IF;
			ELSIF ( EXPEDIENTE.ID_ESTADO_EXP = ESTADO_EXP_CERRADO ) THEN
				IF ( TAREA.IND_REQ_CIERRE = 'S' ) THEN
					IF ( TAREA.ID_TIPO_TAREA <> TAREA_ENTREGA_TRADUCCION AND TAREA.ID_TIPO_TAREA <> TAREA_TRADUCCION_ENTREGA AND TAREA.ID_TIPO_TAREA <> TAREA_CORREDACCION ) THEN
						RETORNO := 1;
					ELSE
						SELECT NVL(MAX(ORDEN_081), 0) INTO ORDEN_TAREA_ENTREGA FROM AA79B81T00
						WHERE ( ID_TIPO_TAREA_081 = TAREA_ENTREGA_TRADUCCION OR ID_TIPO_TAREA_081 = TAREA_TRADUCCION_ENTREGA OR ID_TIPO_TAREA_081 = TAREA_CORREDACCION )
						AND ANYO_081 = TAREA.ANYO AND NUM_EXP_081 = TAREA.NUM_EXP;

						IF ( ORDEN_TAREA_ENTREGA <> 0 AND TAREA.ORDEN < ORDEN_TAREA_ENTREGA ) THEN
							RETORNO := 1;
						END IF;
					END IF;
				END IF;
			END IF;
		ELSIF ( EXPEDIENTE.ID_TIPO_EXPEDIENTE = TIPO_REVISION ) THEN
			IF ( EXPEDIENTE.ID_ESTADO_EXP = ESTADO_EXP_EN_CURSO ) THEN
				IF ( TAREA.ID_TIPO_TAREA = TAREA_ENTREGA_REVISION AND TAREA.IND_NO_CONFORMIDAD = 'S' ) THEN
					RETORNO := 1;
				ELSE
					SELECT COUNT(1) into contador from AA79B81T00 WHERE ID_TAREA_REL_081 = IDTAREA AND ID_TIPO_TAREA_081 = TAREA_NO_CONF_CLIENTE
					AND ESTADO_EJECUCION_081 <> ESTADO_TAREA_EJECUTADA;
					if (CONTADOR > 0) THEN
						RETORNO := 1;
					ELSE
						SELECT NVL(MAX(ORDEN_081), 0) INTO ORDEN_TAREA_ENTREGA FROM AA79B81T00
						WHERE ID_TIPO_TAREA_081 = TAREA_ENTREGA_REVISION AND IND_NO_CONFORMIDAD_081 = 'S' AND ANYO_081 = TAREA.ANYO AND NUM_EXP_081 = TAREA.NUM_EXP;

						IF ( ORDEN_TAREA_ENTREGA <> 0 AND TAREA.ORDEN < ORDEN_TAREA_ENTREGA ) THEN
							RETORNO := 1;
						END IF;
					END IF;
				END IF;
			ELSIF ( EXPEDIENTE.ID_ESTADO_EXP = ESTADO_EXP_CERRADO ) THEN
				IF ( TAREA.IND_REQ_CIERRE = 'S' ) THEN
					IF ( TAREA.ID_TIPO_TAREA <> TAREA_ENTREGA_REVISION ) THEN
						RETORNO := 1;
					ELSE
						SELECT NVL(MAX(ORDEN_081), 0) INTO ORDEN_TAREA_ENTREGA FROM AA79B81T00
						WHERE ID_TIPO_TAREA_081 = TAREA_ENTREGA_REVISION AND ANYO_081 = TAREA.ANYO AND NUM_EXP_081 = TAREA.NUM_EXP;

						IF ( ORDEN_TAREA_ENTREGA <> 0 AND TAREA.ORDEN < ORDEN_TAREA_ENTREGA ) THEN
							RETORNO := 1;
						END IF;
					END IF;
				END IF;
			END IF;
		END IF;
	END IF;

	return RETORNO;
END;
/