CREATE OR REPLACE PROCEDURE CALCULO_IMPORTES_INTER (
   ANYO IN   NUMBER,
   NUM_EXP IN NUMBER
)
IS

IMPORTE NUMBER(9,2):=0.0;
IMPORTE_ENTIDAD NUMBER(9,2):=0.0;
IMPORTE_BASE NUMBER(8,2):=0.0;
IMPORTE_BASE_TOTAL NUMBER(8,2):=0.0;
IMPORTE_BASE_ENTIDAD NUMBER(8,2):=0.0;
IMPORTE_IVA NUMBER(8,2):=0.0;
IMPORTE_IVA_ENTIDAD NUMBER(8,2):=0.0;

TIPO_TAREA_INTERPRETACION NUMBER(2):= 8;

NUM_INTERPRETES NUMBER(2):= 0;
HORAS_INTERPRETACION VARCHAR2(150):='00:00';

HORAS_TOTALES NUMBER(9,2):=0.0;

TYPE r_reg_interpretacion IS RECORD (
      TIPO_ACTO NUMBER(2) NULL,
      TIPO_FACTURACION VARCHAR2(1) NULL,
      IND_PROGRAMADA VARCHAR2(1) NULL,
      TIPO_PETICIONARIO VARCHAR2(1) NULL,
      IND_CAE VARCHAR2(1) NULL
);
INTERPRETACION r_reg_interpretacion;

TYPE r_reg_orden IS RECORD (
     ID_ORDEN NUMBER(4),
    JORN_COMP_HORAS_DESDE NUMBER(1),
    JORN_COMP_HORAS_HASTA NUMBER(1) ,
    JORN_MEDIA_HORAS_DESDE NUMBER(1) ,
    JORN_MEDIA_HORAS_HASTA NUMBER(1) ,
    PREC_MIN_CAE NUMBER(6,2) ,
    PREC_MIN_NO_CAE NUMBER(6,2) ,
    PREC_HORA_REUNION_CAE NUMBER(5,2) ,
    PREC_HORA_REUNION_NO_CAE NUMBER(5,2) ,
    PREC_JORN_CONGRESO_EU NUMBER(6,2) ,
    PREC_MEDIA_CONGRESO_EU NUMBER(6,2) ,
    PREC_HORA_CONGRESO_EU NUMBER(5,2) ,
    PREC_JORN_CONGRESO_NO_EU NUMBER(6,2) ,
    PREC_MEDIA_CONGRESO_NO_EU NUMBER(6,2) ,
    PREC_HORA_CONGRESO_NO_EU NUMBER(5,2),
    POR_IVA NUMBER(3)
);
ORDEN_PRECIOS r_reg_orden;

TIPO_FACTURACION_INTERPRETE VARCHAR2(1):= 'I';
TIPO_FACTURACION_HORA VARCHAR2(1):= 'H';

N_JORNADAS_COMPLETAS NUMBER(3):= 0;
N_JORNADAS_MEDIAS NUMBER(3):= 0;

HORAS NUMBER(7):=0;
MINUTOS NUMBER(2):=0;

PRECIO_MINIMO NUMBER(6,2);

NUEVA_DIRNORA NUMBER(8);

-- SELECCIONAMOS LAS TAREAS DE INTERPREATCION DEL EXPEDIENTE;
CURSOR C_TAREAS IS
(SELECT ID_TAREA_081, HORAS_TAREA_082 FROM AA79B81S01, AA79B82S01 WHERE ID_TAREA_081 = ID_TAREA_082
AND ID_TIPO_TAREA_081 = TIPO_TAREA_INTERPRETACION
AND ANYO_081 = ANYO AND NUM_EXP_081 = NUM_EXP AND ESTADO_EJECUCION_081 = 3);

-- SELECCIONAMOS LAS ENTIDADES DE FACTURACION DEL EXPEDIENTE;
CURSOR C_ENTIDADES_FACT IS
(SELECT ID_058, TIPO_ENTIDAD_ASOC_058, ID_ENTIDAD_ASOC_058, DNI_CONTACTO_058, POR_FACTURA_058, ent.FACTURABLE AS FACTURABLE,
ent.IVA AS IVA, nvl(sol.CDIRNORA,ent.CDIRNORA) AS DIRNORA
FROM AA79B58T00, X54JAPI_ENTIDADES_SOLIC ent, X54JAPI_SOLICITANTES sol
WHERE TIPO_ENTIDAD_ASOC_058 = ent.TIPO AND ID_ENTIDAD_ASOC_058 = ent.CODIGO
AND DNI_CONTACTO_058 = sol.DNI (+)
AND ANYO_058 = ANYO AND NUM_EXP_058 = NUM_EXP);

BEGIN

	-- seleccionamos la orden de precios publicos, lo referente a la interpretación
	select ID_ORDEN_035, JORN_COMP_HORAS_DESDE_035, JORN_COMP_HORAS_HASTA_035, JORN_MEDIA_HORAS_DESDE_035, JORN_MEDIA_HORAS_HASTA_035 ,
    PREC_MIN_CAE_035, PREC_MIN_NO_CAE_035, PREC_HORA_REUNION_CAE_035, PREC_HORA_REUNION_NO_CAE_035, PREC_JORN_CONGRESO_EU_035,
    PREC_MEDIA_CONGRESO_EU_035,  PREC_HORA_CONGRESO_EU_035, PREC_JORN_CONGRESO_NO_EU_035, PREC_MEDIA_CONGRESO_NO_EU_035,
    PREC_HORA_CONGRESO_NO_EU_035, POR_IVA_031 into ORDEN_PRECIOS from AA79B35T00, AA79B30T00, AA79B31T00
    WHERE ID_030 = ID_ORDEN_031 AND ID_030 = ID_ORDEN_035 (+) AND IND_VIGOR_030 = 'S' AND IND_VIGENTE_031 = 'S';

    -- obtenemos el tipo de acto
	SELECT TIPO_ACTO_052, TIPO_FACTURACION_008, IND_PROGRAMADA_052, TIPO_PETICIONARIO_052, DECODE(CODPROV_049,'20','S','48','S','01','S','N')  into INTERPRETACION
	FROM AA79B52T00, AA79B08T00, AA79B49T00 WHERE ANYO_052 = ANYO AND NUM_EXP_052 = NUM_EXP AND TIPO_ACTO_052 = ID_008 AND DIR_NORA_052 = CDIRNORA_049;


	FOR R_TAREAS IN C_TAREAS LOOP
		NUM_INTERPRETES:= NUM_INTERPRETES + 1;
		IF (HORAS_INTERPRETACION = '00:00') THEN
			HORAS_INTERPRETACION:= R_TAREAS.HORAS_TAREA_082;
		ELSE
			HORAS_INTERPRETACION:= HORAS_INTERPRETACION ||', '|| R_TAREAS.HORAS_TAREA_082;
		END IF;

		HORAS:= TO_NUMBER(SUBSTR(R_TAREAS.HORAS_TAREA_082, 1, INSTR(R_TAREAS.HORAS_TAREA_082, ':')-1));
		MINUTOS:= TO_NUMBER(SUBSTR(R_TAREAS.HORAS_TAREA_082, INSTR(R_TAREAS.HORAS_TAREA_082, ':')+1));
		IF (MINUTOS > 0 ) THEN
			HORAS:= HORAS + 1;
		END IF;

		IF (INTERPRETACION.TIPO_FACTURACION = TIPO_FACTURACION_INTERPRETE) THEN
			-- Miramos cuantas jornadas completas enteras hay según el rango de jornadas completas definido.
			N_JORNADAS_COMPLETAS := TRUNC(HORAS/ORDEN_PRECIOS.JORN_COMP_HORAS_HASTA);
			IF (N_JORNADAS_COMPLETAS = 0) THEN
			 -- comprobamos si son horas dentro del rango de jornada completa
			 IF (HORAS >= ORDEN_PRECIOS.JORN_COMP_HORAS_DESDE AND HORAS <= ORDEN_PRECIOS.JORN_COMP_HORAS_HASTA) THEN
			 	N_JORNADAS_COMPLETAS := 1;
			 	HORAS:= 0; -- no quedan horas pendientes de tarificar
			 END IF;
			ELSE
				HORAS:= HORAS - (N_JORNADAS_COMPLETAS * ORDEN_PRECIOS.JORN_COMP_HORAS_HASTA);
			END IF;
			-- miramos cuantas medias jornadas hay
			N_JORNADAS_MEDIAS := TRUNC(HORAS/ORDEN_PRECIOS.JORN_MEDIA_HORAS_HASTA);
			IF (N_JORNADAS_MEDIAS = 0) THEN
				-- comprobamos si son horas dentro del rango de jornada media
				IF (HORAS >= ORDEN_PRECIOS.JORN_MEDIA_HORAS_DESDE AND HORAS <= ORDEN_PRECIOS.JORN_MEDIA_HORAS_HASTA) THEN
			 		N_JORNADAS_MEDIAS := 1;
			 		HORAS:= 0; -- no quedan horas pendientes de tarificar
			 	END IF;
			ELSE
				HORAS:= HORAS - (N_JORNADAS_MEDIAS * ORDEN_PRECIOS.JORN_MEDIA_HORAS_HASTA);
			END IF;

			IF (INTERPRETACION.IND_PROGRAMADA = 'S' AND INTERPRETACION.TIPO_PETICIONARIO = 'A') THEN
				IMPORTE:= IMPORTE + ((N_JORNADAS_COMPLETAS*ORDEN_PRECIOS.PREC_JORN_CONGRESO_EU) + (N_JORNADAS_MEDIAS*ORDEN_PRECIOS.PREC_MEDIA_CONGRESO_EU) + (HORAS*ORDEN_PRECIOS.PREC_HORA_CONGRESO_EU));
			ELSE
				IMPORTE:= IMPORTE + ((N_JORNADAS_COMPLETAS*ORDEN_PRECIOS.PREC_JORN_CONGRESO_NO_EU) + (N_JORNADAS_MEDIAS*ORDEN_PRECIOS.PREC_MEDIA_CONGRESO_NO_EU) + (HORAS*ORDEN_PRECIOS.PREC_HORA_CONGRESO_NO_EU));
			END IF;
		ELSE
			IF (INTERPRETACION.IND_CAE = 'S') THEN
				IMPORTE:= IMPORTE + (HORAS*ORDEN_PRECIOS.PREC_HORA_REUNION_CAE);
			ELSE
				IMPORTE:= IMPORTE + (HORAS*ORDEN_PRECIOS.PREC_HORA_REUNION_NO_CAE);
	    	END IF;
		END IF;
	END LOOP;

	IF (INTERPRETACION.IND_CAE = 'S') THEN
		IF (IMPORTE < ORDEN_PRECIOS.PREC_MIN_CAE) THEN
			IMPORTE:= ORDEN_PRECIOS.PREC_MIN_CAE;
			PRECIO_MINIMO := ORDEN_PRECIOS.PREC_MIN_CAE;
		END IF;
	ELSE
		IF (IMPORTE < ORDEN_PRECIOS.PREC_MIN_NO_CAE) THEN
			IMPORTE:= ORDEN_PRECIOS.PREC_MIN_NO_CAE;
			PRECIO_MINIMO := ORDEN_PRECIOS.PREC_MIN_NO_CAE;
		END IF;
	END IF;

	--if (NUM_INTERPRETES > 0) THEN
	IMPORTE_BASE_TOTAL:= round((IMPORTE * 100 / (100 + ORDEN_PRECIOS.POR_IVA)),3);

	--END IF;

	IMPORTE_IVA:= 0;
	IMPORTE:= 0;
	IMPORTE_BASE:= 0;

	FOR R_ENTIDADES_FACT IN C_ENTIDADES_FACT LOOP

		IMPORTE_BASE_ENTIDAD:= round((IMPORTE_BASE_TOTAL * R_ENTIDADES_FACT.POR_FACTURA_058)/100,2);
		IMPORTE_IVA_ENTIDAD:= 0.0;
		IF (R_ENTIDADES_FACT.IVA = 'S') THEN
			IMPORTE_IVA_ENTIDAD:= (IMPORTE_BASE_ENTIDAD * ORDEN_PRECIOS.POR_IVA) / 100;
		END IF;
		IMPORTE_ENTIDAD:= IMPORTE_IVA_ENTIDAD + IMPORTE_BASE_ENTIDAD;

		IMPORTE_BASE      := IMPORTE_BASE       + IMPORTE_BASE_ENTIDAD;
		IMPORTE_IVA:= IMPORTE_IVA + IMPORTE_IVA_ENTIDAD;
		IMPORTE:= IMPORTE + IMPORTE_ENTIDAD;

		-- copiamos la dirección NORA a nuestra tabla de direcciones
		SELECT AA79B49Q00.nextVal into NUEVA_DIRNORA from dual;
		INSERT into AA79b49T00 (CDIRNORA_049, TIPONORA_049, CODNORA_049, CODPROV_049, CODMUNI_049, CODLOCAL_049, CODPOST_049,
		ESCALER_049, PISO_049, MANO_049, PUERTA_049, APROX_049, TXTCALLE_049, TXTPORTAL_049, CODPAIS_049, TXTPROV_049, TXTCIUDAD_049,
		TXTDIREC_049) SELECT NUEVA_DIRNORA, TIPONORA, CODPORTAL, CODPROVINCIA, CODMUNICIPIO, CODLOCALIDAD, CODPOSTAL, ESCALERA, PISO, MANO,
		PUERTA, APROX, CALLE, PORTAL, CODPAIS, PROVINCIA, MUNICIPIO, DIRECCION FROM X54JNORA WHERE CDIRNORA = R_ENTIDADES_FACT.DIRNORA;

		INSERT INTO AA79BA6T00 (ID_0A6, DIR_NORA_0A6, IND_FACTURABLE_0A6, IND_IVA_0A6, IMPORTE_BASE_0A6, IMPORTE_IVA_0A6, IMPORTE_TOTAL_0A6)
		VALUES (R_ENTIDADES_FACT.ID_058, NUEVA_DIRNORA, R_ENTIDADES_FACT.FACTURABLE, R_ENTIDADES_FACT.IVA, IMPORTE_BASE_ENTIDAD, IMPORTE_IVA_ENTIDAD, IMPORTE_ENTIDAD );
	END LOOP;

	INSERT INTO AA79BA3T00 (ANYO_0A3, NUM_EXP_0A3, ID_ORDEN_0A3, POR_IVA_0A3, PRECIO_MINIMO_0A3,
	NUM_INTERPRETES_0A3, HORAS_INTERPRETACION_0A3, IMPORTE_BASE_0A3, IMPORTE_IVA_0A3, IMPORTE_TOTAL_0A3, IND_REVISADO_0A3, TIPO_FACTURACION_0A3)
	VALUES(ANYO, NUM_EXP,ORDEN_PRECIOS.ID_ORDEN, ORDEN_PRECIOS.POR_IVA, PRECIO_MINIMO, NUM_INTERPRETES,
	HORAS_INTERPRETACION, round(IMPORTE_BASE,2), IMPORTE_IVA, IMPORTE, 'S', INTERPRETACION.TIPO_FACTURACION);

END;
/